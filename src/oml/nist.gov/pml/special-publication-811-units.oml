@dc:creator "Jet Propulsion Laboratory"
@dc:rights """Copyright 2010-2020, by the California Institute of Technology. ALL RIGHTS RESERVED. United States Government Sponsorship acknowledged. Any commercial use must be negotiated with the Office of Technology Transfer at the California Institute of Technology. 
This software may be subject to U.S. export control laws. By accepting this software, the user agrees to comply with all applicable U.S. export laws and regulations. User has the responsibility to obtain export licenses, or other export authority as may be required before exporting such information to foreign countries or providing access to foreign persons."""
@dc:^source "https://www.nist.gov/pml/special-publication-811/nist-guide-si-appendix-b-conversion-factors/nist-guide-si-appendix-b8"
@dc:^source "https://en.wikipedia.org/wiki/English_Engineering_Units"
@dc:^description "English Engineering Units defined in NIST PML Special Publication 811, Appendix B.8"
@dc:title "Special Publication 811 Units"
vocabulary <http://nist.gov/pml/special-publication-811-units> with # as special-publication-811-units {

	extends <http://iso.org/80000-5-units>

	@dc:^description "1 foot = 3.048 E-01 metre"
	@dc:^source "https://en.wikipedia.org/wiki/Foot_(unit)"
	concept foot :> 80000-3-units:LengthUnit, metrology:NonCoherentDerivedMeasurementUnit [
		restricts all relation metrology:nonCoherentDerivedMeasurementUnitOf to 80000-3-units:metre
	]

	@dc:^description "1 pound-mass = 4.535 923 7 E-01 kilogram"
	@dc:^source "https://en.wikipedia.org/wiki/Pound_(mass)"
	concept pound-mass :> 80000-4-units:MassUnit, metrology:NonCoherentDerivedMeasurementUnit [
		restricts all relation metrology:nonCoherentDerivedMeasurementUnitOf to 80000-4-units:kilogram
	]

	@dc:^description "1 pound-force = 4.448 221 615 260 5 E+00 newton"
	@dc:^source "https://en.wikipedia.org/wiki/Pound_(force)"
	concept pound-force :> 80000-4-units:ForceUnit, metrology:NonCoherentDerivedMeasurementUnit [
		restricts all relation metrology:nonCoherentDerivedMeasurementUnitOf to 80000-4-units:newton
	]

	@dc:^description "1 pound-force-second = 4.448 221 615 260 5 E+00 newton-second"
	concept pound-force-second :> 80000-4-units:ImpulseUnit, metrology:DerivedMeasurementUnit

	relation entity Pound-Force-Second-Has-Pound-Force :> metrology:HasFactorForMeasurementUnit [
		from pound-force-second
		to pound-force
		forward pound-force-second-has-pound-force
		restricts scalar property metrology:exponent to "1/1"^^owl:rational
	]

	relation entity Pound-Force-Second-Has-Second :> metrology:HasFactorForMeasurementUnit [
		from pound-force-second
		to 80000-3-units:second
		forward pound-force-second-has-second
		restricts scalar property metrology:exponent to "1/1"^^owl:rational
	]
 
	@dc:^source "https://en.wikipedia.org/wiki/Fahrenheit"
	concept degreeFahrenheit :> 80000-5-units:CelciusTemperatureUnit [
		restricts scalar property metrology:nonCoherentRelationship to "t/°C = (t/°F - 32)/1.8"^^xsd:string 
		restricts relation metrology:nonCoherentDerivedMeasurementUnitOf to 80000-5-instances:degreeCelcius
	]
 
	@dc:^source "https://en.wikipedia.org/wiki/Rankine_scale"
	concept degreeRankine :> metrology:NonCoherentDerivedMeasurementUnit [
		restricts scalar property metrology:nonCoherentRelationship to "T/K = (T/°R)/1.8"^^xsd:string 
		restricts relation metrology:nonCoherentDerivedMeasurementUnitOf to 80000-5-instances:kelvin
	]
}