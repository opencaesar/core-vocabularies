@dc:title "Metrology vocabulary adapted from iupac.org"
@dc:^source "https://ontology.iupac.org/"
@dc:^source "https://ontology.iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf"
@dc:^source "https://jcgm.bipm.org/vim/en/index.html"
@dc:^description "This vocabulary is a partial account of Prof. Rene Dybkaer's 'An Ontology of Property' 
focused on the concepts necessary for dimensional analysis of systems of metrological units and
of systems of quantities based on the algebraic relationships involved in the
definitions of the metrological units and the kinds of quantities in these respective systems.

See the rationale behind the 3rd revision of the Vocabulary of International Metrology, VIM3,
for references to Prof. Dybkaer's influential work: https://www.bipm.org/en/publications/guides/rationale-vim3.html

Where possible, correspondences to VIM3 definitions will be referenced from https://jcgm.bipm.org/vim/en/index.html
The VIM3 is available in document PDF here: https://www.bipm.org/utils/common/documents/jcgm/JCGM_200_2012.pdf

References to definitions in the ontology are indicated via a reference 
in parentheses with a chapter and section and a page number;
for example, (S.2.23, p. 33) refers to the definition of 'object' on page 33."
@dc:rights "Copyright 2019, by the California Institute of Technology. ALL RIGHTS RESERVED.
United States Government Sponsorship acknowledged. 
Any commercial use must be negotiated with the Office of Technology Transfer at the California Institute of Technology.

This software may be subject to U.S. export control laws. 
By accepting this software, the user agrees to comply with all applicable U.S. export laws and regulations. 
User has the responsibility to obtain export licenses, or other export authority as may be required before exporting such information to foreign countries or providing access to foreign persons."
@dc:creator "Jet Propulsion Laboratory, California Institute of Technology"
@dc:hasVersion "2.0.0"
@dc:date "2020-11-09"
vocabulary <http://iupac.org/metrology> with # as metrology {

	extends <http://www.w3.org/2002/07/owl>

	@dc:^description "metrology:Thing is the root of the taxonomy of OML entities involved in metrological modeling either as an object
    or a metrological characterization of an object."
	aspect Thing

	@dc:^description "metrology:MetrologicalEntity is the root of the taxonomy of OML entities involved in the vocabulary
    for metrological characterization of an object."
	aspect MetrologicalEntity :> Thing

	aspect ExponentFactor :> Thing

	scalar property exponent [
		domain ExponentFactor
		range owl:rational
		functional
	]

	// Definitions from Chapter 2: Terminology and 3: "System" and "Component"
	
	@dc:^description "A metrology:Object is anything perceivable or conceivable.

    A metrology:Object serves two purposes:
    1) Specify the subject of a metrology:isQuantityOf for a given metrology:Quantity.
    2) Provide a reference for elaborating a rich description elsewhere using an appropriate vocabulary.

    Note: Dybkaer's ontology defines additional vocabulary about *object* (S.2.24) with the subconcepts of 
    *system* (S.3.3) and *component* (S.3.4), which are further divided in terms of
    *element*, *relationship*,  and *process* (see Figure 3.5, p. 41).

    Since none of the subconcepts of *object* (S.2.23) provide any additional semantics to the metrology vocabulary,
    they are omitted here. This ommision simplifies the task of establishing correspondences between metrology:Object
    in this vocabulary and rich descriptions elsewhere constructed using an appropriate vocabulary.
    "
	@dc:^source "object [https://www.iso.org/obp/ui/#iso:std:iso:1087:ed-2:v1:en, 3.1.1]"
	@dc:^source "object [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 2.23, p. 33]"
	aspect Object :> Thing

	// Definitions from Chapter 4: Current meaning of "quantity".
	
	@dc:^description "A reference can be a measurement unit, a measurement procedure, or a reference material, or a combination of such."
	@dc:^source "[https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 4.10, p. 44]"
	aspect Reference :> MetrologicalEntity

	// Definitions from Chapter 5: "Property", "examinand", and "measurand".
	
	@dc:^source "reference material [https://jcgm.bipm.org/vim/en/5.13.html]"
	@dc:^source "reference material [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 5.1, p 47]"
	aspect ReferenceMaterial :> Reference

	// Definitions from Chapters 6,7,8,9,10,11: None
	// Definitions from Chapter 12: Generic concept system on <property> and <quantity>
	// and from Chapter 13: Generic concept system on <kind-of-property> including <kind-of-quantity>.
	
	@dc:^description "A metrology:Quantity is an inherent state- or process-descriptive feature
    of a metrological:Object that has a magnitude expressed as a metrological:Magnitude.

    This definition is adapted from the expanded definition of 'quantity' (S.12.13, S.12.14).

	Dybkaer's ontology, makes a distinction between Quantity (S.12.13) and KindOfQuantity (S.13.3.1)
	where the latter is defined as the 'common defining aspect of mutually comparable properties having a magnitude'.
	
	In this vocabulary, a specialization of metrology:Quantity corresponds to Dybkaer's KindOfQuantity per S.13.3.1 and S.13.4.1
	whereas an instance of metrology:Quantity corresponds to Dybkaer's Quantity per S.12.13 and S.12.14.
	
	Note that Dybkaer's associative relation between Quantity and KindOfQuantity corresponds to the classification
	relationship between, respectively, an instance of metrology:Quantity and a specialization of metrology:Quantity.
	
	Defining metrology:Quantity as an OML Concept instead of an OML Aspect or Relation, enables the bundle closure 
	algorithm to infer disjunctions for specializations of metrology:Quantity such as the ISO 80000 libraries
	or user-defined vocabularies."
	@dc:^source "quantity [https://jcgm.bipm.org/vim/en/1.1.html]"
	@dc:^source "quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 12.13, p 115]"
	@dc:^source "kind of quantity [https://jcgm.bipm.org/vim/en/1.2.html]"
	@dc:^source "kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.3.1, p. 125 and 13.4.1, p. 126]"
	@dc:^source "[https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 12.13, p 115]"
	@dc:^source "[https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.6, p 128]"
	concept Quantity :> MetrologicalEntity

	@dc:^description "Specifies the metrology:Object that the metrology:Quantity is a feature of.
    Although every metrology:Quantity must have exactly one metrology:Object as its metrology:isQuantityOf,
    this relationship, metrology:isQuantityOf, can be specified either explicitly as an asserted axiom
    or implicitly as a constraint such as a range restriction or an inference rule."
	@dc:^source "quantity [https://jcgm.bipm.org/vim/en/1.1.html]"
	@dc:^source "quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 12.13, p 115]"
	relation entity IsQuantityOf [
		from Quantity
		to Object
		forward isQuantityOf
		reverse hasQuantity
		functional
	]

	@dc:^description "Specifies the metrology:Magnitude that is the magnitude of a metrology:Quantity.
    This relationship can be specified either explicitly as an asserted axiom
    or implicitly as a constraint such as a range restriction or an inference rule
	involving the foward or reverse property."
	@dc:^source "quantity [https://jcgm.bipm.org/vim/en/1.1.html]"
	@dc:^source "quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 12.13, p 115]"
	relation entity IsMagnitudeOf [
		from Magnitude
		to Quantity
		forward isMagnitudeOf
		reverse hasMagnitude
	]

	@dc:^description "metrology:IsReflexiveMagnitudeOf defines a convenient pattern that combines in a single 
	instantiable relation two entities that would otherwise have to be instantiated separately,i.e., an instance 
	of metrology:Magnitude and an instance of metrology:HasMagnitude. This relation entity is unsual because it 
	is also its source."
	relation entity IsReflexiveMagnitudeOf :> metrology:IsMagnitudeOf, metrology:Magnitude [
		from IsReflexiveMagnitudeOf
		to metrology:Quantity
		forward isReflexiveMagnitudeOf
	]

	@dc:^description "An instance of metrology:OrdinalQuantity is a metrology:Quantity defined by a measurement procedure (S.14.4.4)
    that can be stated only to be lesser than, equal to, or greater than another quantity of the same metrology:KindOfQuantity.
	
	As a class, metrology:OrdinalQuantity classifies metrology:Quantity(ies) that are rankable by magnitude, but are neither subtractive nor divisible (S.13.3.2)."
	@dc:^source "ordinal quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 12.16, p 116]"
	@dc:^source "ordinal kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.3.2, p. 125]"
	concept OrdinalQuantity :> Quantity [
		restricts all relation hasMagnitude to OrdinalMagnitude
	]

	@dc:^description "An instance of metrology:UnitaryQuantity is a metrology:Quantity with a magnitude expressed as a reference quantity 
    multiplied by a number (S.12.17).

	As a class, of metrology:UnitaryQuantity classifies metrology:Quantity(ies) with magnitudes expressed as a measurement unit multiplied by a number (S.13.3.3)."
	@dc:^source "unitary quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 12.17, p 116-7]"
	@dc:^source "unitary kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.3.3, p. 125]"
	concept UnitaryQuantity :> Quantity [
		restricts all relation hasMagnitude to UnitaryMagnitude
	]

	@dc:^description "A metrology:IndependentUnitaryQuantity is a metrology:UnitaryQuantity defined independently of any other metrology:UnitaryQuantity."
	@dc:^source "implicit as a quantity that is not a derived quantity [https://jcgm.bipm.org/vim/en/1.5.html]"
	@dc:^source "base unitary kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.9, p. 130]"
	concept IndependentUnitaryQuantity :> UnitaryQuantity

	@dc:^description "A metrology:DerivedUnitaryQuantity is a metrology:UnitaryQuantity defined as the product of powers of metrology:UnitaryQuantity."
	@dc:^source "derived quantity [https://jcgm.bipm.org/vim/en/1.5.html]"
	@dc:^source "derived unitary kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.11, p. 130-1]"
	concept DerivedUnitaryQuantity :> UnitaryQuantity

	@dc:^description "In dimensional calculus, a metrology:DerivedUnitaryQuantity algebraically depends on a metrology:UnitaryQuantity as a product factor raised to a rational exponent."
	@dc:^source "derived quantity [https://jcgm.bipm.org/vim/en/1.5.html]"
	@dc:^source "derived unitary kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.11, p. 130-1]"
	relation entity HasFactorForUnitaryQuantity :> ExponentFactor [
		from DerivedUnitaryQuantity
		to UnitaryQuantity
		forward hasFactorForUnitaryQuantity
		irreflexive
	]

	@dc:^description "metrology:isDimensionlessQuantity specifies whether the definition of a metrology:UnitaryQuantity has dimension 'one',
    either intrisically as an metrology:IndependentUnitaryQuantity 
    or extrinsically as a metrology:DerivedUnitaryQuantity where the product factors of other metrology:UnitaryQuantity(ies) could be algebraically reduced to 'one'.
    
    Note when defining a metrology:DerivedUnitaryQuantity with metrology:isDimensionlessQuantity=true, 
    it is important to retain the non-simplified product factors in order to preserve the metrological provenance of the metrology:DerivedUnitaryQuantity
    with respect to other metrology:UnitaryQuantity(ies)."
	@dc:^source "See Note 1 in https://jcgm.bipm.org/vim/en/1.8.html"
	scalar property isDimensionlessQuantity [
		domain UnitaryQuantity
		range xsd:boolean
		functional
	]

	@dc:^description "A metrology:SystemOfUnitaryQuantities is a collection of metrology:UnitaryQuantity(ies) that can be partitioned into two groups:
    base and derived. Note that this partitioning is independent of how the algebraic definition of each metrology:UnitaryQuantity
    (i.e metrology:IndependentUnitaryQuantity or metrology:DerivedUnitaryQuantity).
    
    Applying dimensional analysis to a metrology:SystemOfUnitaryQuantities means applying the rules of dimensional calculus to the alegbraic definitions 
    required for expressing each derived metrology:UnitaryQuantity of the system as a product of powers involving only the base metrology:UnitaryQuantity of the system."
	@dc:^source "system of quantities [https://jcgm.bipm.org/vim/en/1.3.html]"
	@dc:^source "system of unitary kinds-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.7, p 129]"
	concept SystemOfUnitaryQuantities :> MetrologicalEntity

	@dc:^description "A metrology:SystemHasUnitaryQuantity specifies that a metrology:SystemOfUnitaryQuantities includes a metrology:UnitaryQuantity."
	@dc:^source "system of quantities [https://jcgm.bipm.org/vim/en/1.3.html]"
	@dc:^source "system of unitary kinds-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.7, p 129]"
	relation entity SystemHasUnitaryQuantity [
		from SystemOfUnitaryQuantities
		to UnitaryQuantity
		forward systemHasUnitaryQuantity
	]

	@dc:^description "It is a property of a metrology:SystemOfUnitaryQuantities to select of a subset of metrology:UnitaryQuantity 
	as being functionally independent of each other as its base metrology:UnitaryQuantity
	such that the other metrology:UnitaryQuantity in the system can be algebraically derived from the base metrology:UnitaryQuantity.
  
	Note that selecting a metrology:UnitaryQuantity as a base or derived for a given system is unrelated to its definition 
	(i.e metrology:IndependentUnitaryQuantity or metrology:DerivedUnitaryQuantity)."
	@dc:^source "system of unitary kinds-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.6, p 128]"
	scalar property isBaseQuantity [
		domain SystemHasUnitaryQuantity
		range xsd:boolean
		functional
	]

	// Definitions from Chapter 14: Generic concept system on <examination procedure>, <measurement procedure>, <examination method>, and <examination principle>.
	
	@dc:^source "measurement procedure [https://jcgm.bipm.org/vim/en/2.6.html]"
	@dc:^source "measurement procedure [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 14.4.3, p 139]"
	aspect MeasurementProcedure :> Reference

	// Definitions from Chapter 15: Generic concept system on <examination>, <measurement>; quantification.
	// Definitions from Chapter 16: Generic concept system on <property value>, <quantity value> and allied concepts.
	
	@dc:^description "metrology:Magnitude is the magnitude of a metrology:Quantity represented by a number and a reference."
	@dc:^source "quantity value [https://jcgm.bipm.org/vim/en/1.19.html]"
	@dc:^source "quantity value [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 16.7 and 16.8, p 162]"
	aspect Magnitude :> MetrologicalEntity

	@dc:^description "metrology:hasDoubleNumber provides the capability to specify the number of a metrology:Magnitude as an IEEE 64-bit floating-point number.
	If this capability is inadequate, the metrology vocabulary can be extended with additional properties for an alternate representation of a number."
	scalar property hasDoubleNumber [
		domain Magnitude
		range xsd:double
		functional
	]

	@dc:^description "metrology:HasReference provides the capability to specify the reference involved in measuring the magnitue of a metrology:Quantity."
	relation entity HasReference [
		from Magnitude
		to Reference
		forward hasReference
		functional
	]

	@dc:^source "ordinal quantity value [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 16.9, p 163]"
	aspect OrdinalMagnitude :> Magnitude [
		restricts all relation hasReference to MeasurementProcedure
	]

	@dc:^source "unitary quantity value [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 16.10, p 163]"
	aspect UnitaryMagnitude :> Magnitude [
		restricts all relation hasReference to MeasurementUnit
	]

	// Definitions from Chapter 17: None
	// Definitions from Chapter 18: Mixed concept system on <metrological unit> and <system of metrological units>; the metrological unit "one" and SI units revisited.
	
	@dc:^description "Defining metrology:MeasurementUnit as an OML Concept instead of an OML Aspect or Relation, enables the bundle closure 
	algorithm to infer disjunctions for specializations of metrology:MeasurementUnit such as the ISO 80000 libraries
	or user-defined vocabularies."
	@dc:^source "measurement unit [https://jcgm.bipm.org/vim/en/1.9.html]"
	@dc:^source "metrological unit, measurement unit [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.12, p 189]"
	concept MeasurementUnit :> Reference

	@dc:^description "A metrology:NonCoherentDerivedMeasurementUnit is a metrology:MeasurementUnit 
	that has a proportionality factor different than one with respect to another metrology:MeasurementUnit."
	@dc:^source "non-coherent derived measurement unit [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.20, p 194]"
	concept NonCoherentDerivedMeasurementUnit :> MeasurementUnit

	@dc:^description "metrology:NonCoherentDerivedMeasurementUnitOf specifies the metrology:MeasurementUnit that a metrology:NonCoherentDerivedMeasurementUnit derives from."
	@dc:^source "non-coherent derived measurement unit [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.20, p 194]"
	relation entity NonCoherentDerivedMeasurementUnitOf [
		from NonCoherentDerivedMeasurementUnit
		to MeasurementUnit
		forward nonCoherentDerivedMeasurementUnitOf
		irreflexive
		functional
	]

	@dc:^description "metrology:nonCoherentRelationship specifies the non-coherent derivation relationship between a metrology:NonCoherentDerivedMeasurementUnit 
	and a metrology:MeasurementUnit that it is a metrology:nonCoherentDerivedMeasurementUnitOf"
	@dc:^source "non-coherent derived measurement unit [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.20, p 194]"
	scalar property nonCoherentRelationship [
		domain NonCoherentDerivedMeasurementUnit
		range xsd:string
		functional
	]

	@dc:^description "A metrology:PrefixedMeasurementUnit is a metrology:MeasurementUnit defined by multiplication of another metrology:MeasurementUnit (that it must specialize) and a prefix."
	@dc:^source "multiple of a unit 3.17, submultiple of a unit 3.18 [https://www.iso.org/obp/ui/#iso:std:iso:80000:-1:ed-1:v1:en]"
	concept PrefixedMeasurementUnit :> MeasurementUnit

	@dc:^description "A metrology:Prefix is an opaque representation of a multiplier used for defining a metrology:PrefixedMeasurementUnit.
    This representation facilitates dimensional analysis of a metrology:SystemOfMetrologicalUnits as a logical analysis since prefixes are individuals.
    Had the prefix multipler been defined as a scalar property typed by a datatype, dimensional analysis would be a 
    more complicated analysis involving a combination of logical and numerical analysis techniques."
	@dc:^source "international system of units 3.16 [https://www.iso.org/obp/ui/#iso:std:iso:80000:-1:ed-1:v1:en]"
	aspect Prefix :> MetrologicalEntity

	relation entity HasPrefix [
		from PrefixedMeasurementUnit
		to Prefix
		forward hasPrefix
		functional
	]

	@dc:^description "A metrology:CoherentMeasurementUnit is a metrology:MeasurementUnit that has a proportionality factor of one and may have a dimension of one."
	@dc:^source "coherent derived metrological unit [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.19, p 193]"
	concept CoherentMeasurementUnit :> MeasurementUnit

	@dc:^description "metrology:isDimensionlessMeasurementUnit specifies whether the definition of a metrology:CoherentMeasurementUnit has dimension 'one',
    either intrisically as an metrology:IndependentMeasurementUnit 
    or extrinsically as a metrology:DerivedMeasurementUnit where the product factors of other metrology:MeasurementUnit(s) could be algebraically reduced to 'one'.
    
    Note when defining a metrology:DerivedMeasurementUnit with metrology:isDimensionlessMeasurementUnit=true, 
    it is important to retain the non-simplified product factors in order to preserve the metrological provenance of the metrology:DerivedMeasurementUnit
    with respect to other metrology:MeasurementUnit(same)."
	scalar property isDimensionlessMeasurementUnit [
		domain CoherentMeasurementUnit
		range xsd:boolean
		functional
	]

	@dc:^description "A metrology:IndependentMeasurementUnit is a metrology:CoherentMeasurementUnit defined independently of any other metrology:MeasurementUnit(s)."
	@dc:^source "base unit [https://jcgm.bipm.org/vim/en/1.10.html]"
	@dc:^source "base metrological unit, base unit [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.15, p 190-1]"
	concept IndependentMeasurementUnit :> CoherentMeasurementUnit

	@dc:^description "A metrology:DerivedMeasurementUnit is a metrology:CoherentMeasurementUnit defined as the product of powers of other metrology:MeasurementUnit(s).
    If a metrology:DerivedMeasurementUnit has dimension '1', then the factors simplify to one but must retain the provenance of the derived unit
    to other metrology:MeasurementUnits. 
    "
	@dc:^source "derived unit [https://jcgm.bipm.org/vim/en/1.11.html]"
	@dc:^source "derived metrological unit, derived unit [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.17, p 192]"
	concept DerivedMeasurementUnit :> CoherentMeasurementUnit

	@dc:^description "In dimensional calculus, a metrology:DerivedMeasurementUnit algebraically depends on a metrology:MeasurementUnit as a product factor raised to a rational exponent."
	@dc:^source "derived unit [https://jcgm.bipm.org/vim/en/1.11.html]"
	@dc:^source "derived metrological unit, derived unit [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.17, p 192]"
	relation entity HasFactorForMeasurementUnit :> ExponentFactor [
		from DerivedMeasurementUnit
		to MeasurementUnit
		forward hasFactorForMeasurementUnit
		irreflexive
	]

	@dc:^description "A metrology:SystemOfMetrologicalUnits is a collection of metrology:MeasurementUnit(s) that can be partitioned into two groups:
    base and derived. Note that this partitioning is independent of how the algebraic definition of each metrology:MeasurementUnit
    (i.e metrology:NonCoherentDerivedMeasurementUnit, metrology:IndependentMeasurementUnit, metrology:DerivedMeasurementUnit or metrology:PrefixedMeasurementUnit)."
	@dc:^source "system of units [https://jcgm.bipm.org/vim/en/1.13.html]"
	@dc:^source "system of metrological units [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.25, p 195]"
	concept SystemOfMetrologicalUnits :> MetrologicalEntity

	@dc:^description "A metrology:SystemHasMetrologicalUnit specifies that a metrology:SystemOfMetrologicalUnits includes a metrology:MeasurementUnit."
	@dc:^source "system of units [https://jcgm.bipm.org/vim/en/1.13.html]"
	@dc:^source "system of metrological units [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.25, p 195]"
	relation entity SystemHasMetrologicalUnit [
		from SystemOfMetrologicalUnits
		to MeasurementUnit
		forward systemHasMetrologicalUnit
	]

	@dc:^description "It is a property of a metrology:SystemOfMetrologicalUnits to select of a subset of metrology:MeasurementUnit 
	as being functionally independent of each other as its base metrology:MeasurementUnit(s)
	such that the other metrology:MeasurementUnit(s) in the system can be algebraically derived from the base metrology:MeasurementUnit(s).
  
	Note that selecting a metrology:MeasurementUnit as a base or derived for a given system is unrelated to its definition 
	(i.e metrology:NonCoherentDerivedMeasurementUnit, metrology:IndependentMeasurementUnit, metrology:DerivedMeasurementUnit or metrology:PrefixedMeasurementUnit)."
	scalar property isBaseMetrologicalUnit [
		domain SystemHasMetrologicalUnit
		range xsd:boolean
		functional
	]
	
}