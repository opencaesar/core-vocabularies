@dc:title "Metrology"
@dc:type "Foundation"
@dc:^description "The Metology vocabulary defines the concepts of metrology according to VIM3.
See https://jcgm.bipm.org/vim/en/index.html
See https://www.bipm.org/utils/common/documents/jcgm/JCGM_200_2012.pdf

VIM3 was the basis for the updated SysML v1 QUDV library.
VIM3 is also the basis for the revised SysML v2 QUDV library currently developed by the SysML v2 SST;
which is also the basis of the present OML QUDV vocabulary.

The objective is to align this OML QUDV library with the SysMLv2 QUDV library 
such that their respective semantics become interoperable in a practically meaningful way.
That is, the goal is to achieve a level of semantic compatibility such that
the representation of metrology in OML or in SysMLv2 becomes a difference of concrete syntax representation
(i.e, in OML or in Alf) with an equivalent logical semantics in OWL2 DL + SWRL and in SysMLv2's logical semantics."
@dc:rights "Copyright 2019, by the California Institute of Technology. ALL RIGHTS RESERVED. United States Government Sponsorship acknowledged. Any commercial use must be negotiated with the Office of Technology Transfer at the California Institute of Technology.\n\nThis software may be subject to U.S. export control laws. By accepting this software, the user agrees to comply with all applicable U.S. export laws and regulations. User has the responsibility to obtain export licenses, or other export authority as may be required before exporting such information to foreign countries or providing access to foreign persons."
@owl:versionInfo "0.0.1"
@dc:creator "Jet Propulsion Laboratory, in collaboration with the SysMLv2 SST metrology group"
@dc:date "2019-12-15"
vocabulary <http://imce.jpl.nasa.gov/foundation/metrology> with # as metrology {

    extends <http://www.w3.org/2002/07/owl>

    @dc:^description "metrology:Thing is the root of the taxonomy of OML entities involved in metrological modeling.

    metrology:Thing is defined as an OML aspect with the intent that it should be the disjoint union
    of the toplevel categories of entities involved in metrological modeling."
    aspect Thing

    @dc:^description "metrology:MetrologicalEntity is the root of the toplevel category of OML concepts corresponding to VIM3 definitions.
    
    Note: The SysML 1.4 QUDV library is sufficiently expressive to cover VIM3 chapter 1
    (See: http://www.omg.org/cgi-bin/doc?syseng/2014-03-08). Even though the QUDV constructs
    are definied as SysML Blocks, the modeling in OML involves different kinds of constructs, including
    OML concepts and relation entities. Those QUDV constructs that map to OML Concepts are defined
    as specializations of mbase:MetrologicalEntity."
    concept MetrologicalEntity :> Thing

    @dc:^description "metrology:Component is the root of the taxonomy of OML entities corresponding to a phenomenon, body or substance that
    can be the subject of:
    - metrological characterizations via quantity properties 
    - metrological measurement processes for obtaining quantity values for such quantity properties."
    @dc:source "The phenomenon, body or substance referred to in the definition of quantity [https://jcgm.bipm.org/vim/en/1.1.html]"
    @dc:source "component [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 3.4, p. 39]"
    aspect Component :> Thing

    @dc:^description "metrology:Number is the root of the taxonomy of OML entities corresponding to a representation of the numerical value of a quantity [VIM3-1.20].
    A numerical quantity value is an OML entity instead of an OML datatype because it can be referenced in the context of a numerical value equation [VIM3-1.25].
    
    Note: The representation of the numerical quantity value is deliberately left open to be specified in library extensions.
    This strategy provides ample latitude for choosing suitable datatypes for representing a numerical quantity value
    optionally with additional information such as measurement precision [VIM3-2.15].
    "
    @dc:source "numerical quantity value [https://jcgm.bipm.org/vim/en/1.20.html]"
    concept Number :> Thing

    @dc:^description "common defining aspect of mutually comparable properties having a magnitude."
    @dc:source "kind of quantity [https://jcgm.bipm.org/vim/en/1.2.html]"
    @dc:source "kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.3.1, p. 125 and 13.4.1, p. 126]"
    concept QuantityKind :> MetrologicalEntity

    @dc:^description "A QuantityKind for Quantities that are rankable by magnitude, but are neither subtractive nor divisible."
    @dc:source "ordinal kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.3.2, p. 125]"
    concept OrdinalQuantityKind :> QuantityKind

    @dc:^description "A QuantityKind for Quantities with magnitudes expressed as a measurement unit multiplied by a number."
    @dc:source "unitary kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.3.3, p. 125]"
    @dc:source "QuantityKind [https://www.omg.org/spec/SysML/1.6/PDF E.5.2.10, p. 286]"
    concept UnitaryQuantityKind :> QuantityKind

    @dc:source "taxonomic relations among unitary kind-of-quantity is an inherent aspect of the concept system in [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, Table 2.13, p. 28 and Figure 13.13, p. 135]"
    @dc:source "A_generalQuantityKind_specificQuantityKind [https://www.omg.org/spec/SysML/1.6/PDF Figure E.5, p. 279]"
    relation entity HasGeneralQuantityKind [
        from UnitaryQuantityKind
        to UnitaryQuantityKind
        forward hasGeneralQuantityKind
        asymmetric
        irreflexive
    ]

    @dc:^description "A BaseQuantityKind is a QuantityKind that is defined independently of any other UnitaryQuantityKind."
    @dc:source "implicit as a quantity that is not a derived quantity [https://jcgm.bipm.org/vim/en/1.5.html]"
    @dc:source "SimpleQuantityKind [https://www.omg.org/spec/SysML/1.6/PDF E.5.2.13, p. 288]"
    @dc:source "base unitary kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.9, p. 130]"
    @dc:source "SimpleQuantityKind [https://www.omg.org/spec/SysML/1.6/PDF E.5.2.13, p. 288]"
    concept BaseQuantityKind :> UnitaryQuantityKind

    @dc:^description "A DerivedQuantityKind is a UnitaryQuantityKind defined as the product of powers of UnitaryQuantityKinds."
    @dc:source "derived quantity [https://jcgm.bipm.org/vim/en/1.5.html]"
    @dc:source "derived unitary kind-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.11, p. 130-1]"
    @dc:source "DerivedQuantityKind [https://www.omg.org/spec/SysML/1.6/PDF E.5.2.3, p. 282]"
    concept DerivedQuantityKind :> UnitaryQuantityKind

    relation entity HasQuantityFactor [
        from DerivedQuantityKind
        to UnitaryQuantityKind
        forward hasQuantityFactor
        irreflexive
    ]

    scalar property ExponentOfQuantityFactor [
        domain HasQuantityFactor
        range xsd:int
    ]

    @dc:^description "A SystemOfQuantities is a collection of UnitaryQuantityKinds that can be partitioned into two groups:
    base and derived quantity kinds. Note that this partitioning is independent of how the UnitaryQuantityKinds are defined.
    For example, a BaseQuantityKind may be a base quantity kind for one SystemOfQuantities and a derived quantity kind for another one.
    Similarly, a DerivedQuantityKind may be a base quantity kind for one SystemOfQuantities and a derived quantity kind for another one."
    @dc:source "system of quantities [https://jcgm.bipm.org/vim/en/1.3.html]"
    @dc:source "system of unitary kinds-of-quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 13.7, p 129]"
    @dc:source "SystemOfQuantities [https://www.omg.org/spec/SysML/1.6/PDF E.5.2.15, p. 288]"
    concept SystemOfQuantities :> MetrologicalEntity

    relation entity SystemHasQuantityKind [
        from SystemOfQuantities
        to UnitaryQuantityKind
        forward systemHasQuantityKind
    ]

    scalar property IsBaseQuantityKind [
        domain SystemHasQuantityKind
        range xsd:boolean
    ]

    concept Reference :> MetrologicalEntity

    @dc:source "measurement procedure [https://jcgm.bipm.org/vim/en/2.6.html]"
    @dc:source "measurement procedure [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 14.4.3, p 139]"
    concept MeasurementProcedure :> Reference

    @dc:source "reference material [https://jcgm.bipm.org/vim/en/5.13.html]"
    @dc:source "reference material [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 5.1, p 47]"
    concept ReferenceMaterial :> Reference

    @dc:source "measurement unit [https://jcgm.bipm.org/vim/en/1.9.html]"
    @dc:source "metrological unit, measurement unit [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.12, p 189]"
    concept MeasurementUnit :> Reference

    @dc:^description "A SimpleMeasurementUnit is a MeasurementUnit that is defined independently of any other MeasurementUnits."
    @dc:source "base unit [https://jcgm.bipm.org/vim/en/1.10.html]"
    @dc:source "base metrological unit, base unit [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.15, p 190-1]"
    concept SimpleMeasurementUnit :> MeasurementUnit

    @dc:^description "A DerivedMeasurementUnit is a MeasurementUnit that is defined as the product of powers of other MeasurementUnits."
    @dc:source "derived unit [https://jcgm.bipm.org/vim/en/1.11.html]"
    @dc:source "derived metrological unit, derived unit [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.17, p 192]"
    concept DerivedMeasurementUnit :> MeasurementUnit

    relation entity HasUnitFactor [
        from DerivedMeasurementUnit
        to MeasurementUnit
        forward hasUnitFactor
        irreflexive
    ]

    scalar property ExponentOfUnitFactor [
        domain HasUnitFactor
        range xsd:int
    ]

    @dc:source "system of units [https://jcgm.bipm.org/vim/en/1.13.html]"
    @dc:source "system of metrological units [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 18.25, p 195]"
    @dc:source "SystemOfUnits [https://www.omg.org/spec/SysML/1.6/PDF E.5.2.16, p. 295]"
    concept SystemOfUnits :> MetrologicalEntity

    relation entity HasUnit [
        from SystemOfUnits
        to MeasurementUnit
        forward hasUnit
    ]

    scalar property IsBaseUnit [
        domain HasUnit
        range xsd:boolean
    ]

    @dc:source "quantity value [https://jcgm.bipm.org/vim/en/1.19.html]"
    @dc:source "quantity value [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 16.7, p 162]"
    concept QuantityValue :> MetrologicalEntity
    
    relation entity HasNumber [
        from QuantityValue
        to Number
        forward hasNumber
    ]

    relation entity HasReference [
        from QuantityValue
        to Reference
        forward hasReference
    ]

    @dc:source "ordinal quantity value [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 16.9, p 163]"
    concept OrdinalQuantityValue :> QuantityValue [
        restricts all relation hasReference to MeasurementProcedure
    ]

    @dc:source "unitary quantity value [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 16.10, p 163]"
    concept UnitaryQuantityValue :> QuantityValue [
        restricts all relation hasReference to MeasurementUnit
    ]

    @dc:source "quantity [https://jcgm.bipm.org/vim/en/1.1.html]"
    @dc:source "quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 12.13, p 115]"
    relation entity Quantity [
        from Component
        to QuantityValue
        forward quantity
    ]

    relation entity HasQuantityKind [
        from Quantity
        to QuantityKind
        forward hasQuantityKind
    ]

    @dc:source "ordinal quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 12.16, p 116]"
    relation entity OrdinalQuantity :> Quantity [
        from Component
        to OrdinalQuantityValue
        forward ordinalQuantity
        restricts all relation hasQuantityKind to OrdinalQuantityKind
    ]

    @dc:source "unitary quantity [https://iupac.org/wp-content/uploads/2016/01/ontology-on-Property-Division-VII.pdf, 12.17, p 116-7]"
    relation entity UnitaryQuantity :> Quantity [
        from Component
        to UnitaryQuantityValue
        forward unitaryQuantity
        restricts all relation hasQuantityKind to UnitaryQuantityKind
    ]
}